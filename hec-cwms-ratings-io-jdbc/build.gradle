test {
    useJUnitPlatform()
    doLast {
        def dataPaths = test
                .extensions*.findByType(JacocoTaskExtension)
                .destinationFile.absolutePath
        def rootProjectDirUri = rootProject.projectDir.toURI()
        def classpaths = sourceSets.findAll { !it.name.toLowerCase().contains('test') }.flatten()
                .java.outputDir.collect { it.toURI() }
                .collect { rootProjectDirUri.relativize it }
                .collect { "+:$it**" }
        println "##teamcity[jacocoReport " +
                "dataPath='${dataPaths.join ' '}' " +
                "classpath='${classpaths.join ' '}']"
    }
    systemProperty 'cwms.image', project.findProperty('cwms.image')
    systemProperty 'oracle.version', project.findProperty('oracle.version')
    systemProperty 'teamcity.build.branch', project.findProperty('teamcity.build.branch')
}

test.finalizedBy jacocoTestReport

dependencies {
    implementation project(":hec-cwms-ratings-core")
    implementation project(":hec-cwms-ratings-io-xml")
    implementation ("mil.army.usace.hec:cwms-db-jooq-codegen:7.0.0-SNAPSHOT") {
        exclude group: "org.jooq.pro", module: "jooq-meta"
        exclude group: "org.jooq.pro", module: "jooq-codegen"
    }
    implementation("mil.army.usace.hec:cwms-db-dao:9.4.0")
    runtimeClasspath("mil.army.usace.hec:cwms-db-jooq:9.4.0") {
        exclude group: "org.jooq.pro", module: "jooq-meta"
        exclude group: "org.jooq.pro", module: "jooq-codegen"
    }
    testImplementation project(":hec-cwms-ratings-core")
    testImplementation("org.testcontainers:testcontainers:1.17.1")
    testImplementation("org.testcontainers:jdbc:1.17.1")
    testImplementation("org.testcontainers:junit-jupiter:1.17.1")
    testImplementation("mil.army.usace.hec:testcontainers-cwms:1.0.6")
    testImplementation("mil.army.usace.hec:cwms-db-jooq:9.4.0") {
        exclude group: "org.jooq.pro", module: "jooq-meta"
        exclude group: "org.jooq.pro", module: "jooq-codegen"
    }
    annotationProcessor("mil.army.usace.hec:service-annotations:1.2.1")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'hec-cwms-ratings-io-jdbc'
            from components.java
        }
    }
}