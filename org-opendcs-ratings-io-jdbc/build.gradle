plugins {
    id "ratings.java-conventions"
    id "ratings.deps-conventions"
    id "ratings.publishing-conventions"
}

test {
    useJUnitPlatform()
    doLast {
        def dataPaths = test
                .extensions*.findByType(JacocoTaskExtension)
                .destinationFile.absolutePath
        def rootProjectDirUri = rootProject.projectDir.toURI()
        def nonTestSourceSets = sourceSets.findAll { !it.name.toLowerCase().contains('test') }.flatten()
        def classpaths = nonTestSourceSets.collect { it.output.classesDirs.collect { it.toURI() } }.flatten()
                .collect { rootProjectDirUri.relativize it }
                .collect { "+:$it**" }
        println "##teamcity[jacocoReport " +
                "dataPath='${dataPaths.join ' '}' " +
                "classpath='${classpaths.join ' '}']"
    }
    systemProperty 'oracle.version', project.findProperty('oracle.version')
    systemProperties += project.properties.findAll { k, v -> k.startsWith("testcontainer") }
    systemProperties += project.properties.findAll { k, v -> k.startsWith("cwms") }
    systemProperties += project.properties.findAll { k, v -> k.startsWith("teamcity") }
}

test.finalizedBy jacocoTestReport

dependencies {
    implementation project(":org-opendcs-ratings-core")
    implementation (libs.jooq)
    implementation (libs.cwms.db.jooq.codegen) {
        exclude group: "org.jooq", module: "*"
    }
    testImplementation project(":org-opendcs-ratings-core")
    testImplementation(libs.ojdbc)
    testImplementation(libs.testcontainers.testcontainers)
    testImplementation(libs.testcontainers.jdbc)
    testImplementation(libs.testcontainers.junit.jupiter)
    testImplementation(libs.testcontainers.cwms)
}
configurations.testImplementation.extendsFrom(configurations.runtimeOnly)

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'org-opendcs-ratings-io-jdbc'
            from components.java
        }
    }
}