plugins {
    id 'org.sonarqube' version '2.7'
}
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'

version = "0.0.1-SNAPSHOT"

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
    maven {
            url = uri('https://www.hec.usace.army.mil/nexus/repository/maven-public')
        }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation 'mil.army.usace.hec:hec-core:6.0-20210210.230316-12'
    implementation 'mil.army.usace.hec:hec-data:6.0-20210210.230324-12'
    implementation 'mil.army.usace.hec:hec-lib:6.0-20210216.232048-13'
    implementation 'mil.army.usace.hec:hec-rma:6.0-20210210.230339-10'
    implementation 'org.jdom:jdom:1.1'

    implementation 'javax.xml.bind:jaxb-api:2.2.12'
    implementation 'com.sun.xml.bind:jaxb-impl:2.2.11'
    implementation 'com.sun.xml.bind:jaxb-core:2.2.11'
    implementation 'javax.activation:activation:1.1.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
}

test.finalizedBy jacocoTestReport

jacocoTestReport{
        reports {
           xml.enabled true
           html.enabled true
        }
}

task reportCoverage(){
    doLast{
        println "##teamcity[jacocoReport dataPath='$buildDir/jacoco/test.exec']"
    }
}

sonarqube {
        properties {
            property "sonar.sources", "src"
            property "sonar.tests", "src"
            property "sonar.test.inclusions", "**/test/**"
            property "sonar.exclusions", "**/test/**"
            property "sonar.java.coveragePlugin", "jacoco"
            property "sonar.tests", "src/test"
            property "sonar.java.test.binaries", "build/classes/java/test"
            property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'hec-cwms-ratings-intesting'
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username = "$mavenUser"
                password = "$mavenPassword"
            }
            def releasesRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-releases/"
            def snapshotsRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

publish.dependsOn jar