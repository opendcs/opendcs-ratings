plugins {
        id 'org.sonarqube' version '2.7'
        id 'me.qoomon.git-versioning' version '5.1.1'
}

// default property values to save some hassle.
if ( !project.findProperty('mavenUser')) {
    ext.mavenUser = ""
}
if ( !project.findProperty('mavenPassword')) {
    ext.mavenPassword = ""
}

version = "local-SNAPSHOT"

gitVersioning.apply {
    refs {
        branch("(?<version>^[a-zA-Z].+)") {
            version = '${ref.version}-SNAPSHOT'
        }
        tag("(?<version>\\d+\\.\\d+\\.\\d+(?:-[a-zA-Z0-9]*)?)") {
            version = '${ref.version}'
        }
    }
    rev {
        version = '${commit}'
    }
}


subprojects {
    apply plugin: 'base'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'org.sonarqube'

    group = "mil.army.usace.hec"

    // In this section you declare where to find the dependencies of your project
    repositories {
        // Use 'jcenter' for resolving your dependencies.
        // You can declare any Maven/Ivy/file repository here.
        mavenCentral()
        maven {
                url = uri('https://www.hec.usace.army.mil/nexus/repository/maven-public')
        }
    }

    // In this section you declare the dependencies for your production and test code
    dependencies {
        implementation ('mil.army.usace.hec:hec-monolith:4.0.0-RC3') {
            exclude group: "codebase", module: "*"
            exclude group: "com.sun.media", module: "*"
            exclude group: "javax.media", module: "*"
        }

        implementation 'org.jdom:jdom:1.1.3'

        annotationProcessor 'mil.army.usace.hec:service-annotations:1.2.1'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'
    }
    configurations.implementation.extendsFrom(configurations.annotationProcessor)

    java{
            sourceCompatibility = JavaVersion.VERSION_11;
            targetCompatibility = JavaVersion.VERSION_11;
            withJavadocJar()
            withSourcesJar()
    }

    javadoc {
        failOnError = true
        options.addBooleanOption("Xdoclint:all",true)
        options.addBooleanOption("html5",true)
    }

    publishing {

        repositories {
            maven {
                credentials {
                    username = "$mavenUser"
                    password = "$mavenPassword"
                }
                def releasesRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-releases/"
                def snapshotsRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

    publish.dependsOn jar

}