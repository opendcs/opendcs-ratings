plugins {
    id "ratings.java-conventions"
    id "ratings.deps-conventions"
    id "ratings.publishing-conventions"
}

test {
    useJUnitPlatform()
    doLast {
        def dataPaths = test
                .extensions*.findByType(JacocoTaskExtension)
                .destinationFile.absolutePath
        def rootProjectDirUri = rootProject.projectDir.toURI()
        def nonTestSourceSets = sourceSets.findAll { !it.name.toLowerCase().contains('test') }.flatten()
        def classpaths = nonTestSourceSets.collect { it.output.classesDirs.collect { it.toURI() } }.flatten()
                .collect { rootProjectDirUri.relativize it }
                .collect { "+:$it**" }
        println "##teamcity[jacocoReport " +
                "dataPath='${dataPaths.join ' '}' " +
                "classpath='${classpaths.join ' '}']"
    }
}

test.finalizedBy jacocoTestReport

dependencies {
    api(libs.hec.monolith) {
        exclude group: "codebase", module: "*"
        exclude group: "com.sun.media", module: "*"
        exclude group: "javax.media", module: "*"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'hec-cwms-ratings-core'
            from components.java
        }
    }
}