plugins {
    id "ratings.java-conventions"
    id "ratings.deps-conventions"
    id "ratings.publishing-conventions"
}

dependencies {
    compileOnly project(":org-opendcs-ratings-core")

    testImplementation project(":org-opendcs-ratings-core")
}

test {
    useJUnitPlatform()
    doLast {
        def dataPaths = test
                .extensions*.findByType(JacocoTaskExtension)
                .destinationFile.absolutePath
        def rootProjectDirUri = rootProject.projectDir.toURI()
        def nonTestSourceSets = sourceSets.findAll { !it.name.toLowerCase().contains('test') }.flatten()
        def classpaths = nonTestSourceSets.collect { it.output.classesDirs.collect { it.toURI() } }.flatten()
                .collect { rootProjectDirUri.relativize it }
                .collect { "+:$it**" }
        println "##teamcity[jacocoReport " +
                "dataPath='${dataPaths.join ' '}' " +
                "classpath='${classpaths.join ' '}']"
    }
}

test.finalizedBy jacocoTestReport

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'org-opendcs-ratings-io-xml'
            from components.java
        }
    }
}